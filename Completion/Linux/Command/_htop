#compdef htop

_arguments -S : \
  '(-C --no-color --no-colour)'{-C,--no-colo{,u}r}'[monochrome mode]' \
  '(-d --delay)'{-d+,--delay=}'[Set the delay between updates]:duration (tenths of seconds)' \
  '(-F --filter)'{-F+,--Filter=}'[Show only the commands matching the given filter]' \
  '(-)'{-h,--help}'[display usage information]' \
  '(M --no-mouse)'{-M,--no-mouse}'[Disable the mouse]' \
  \*{-p+,--pid=}'[show given pids]: : _sequence -n ${$(</proc/sys/kernel/pid_max)\:-32768} _pids' \
  '(-s --sort-key)'{-s+,--sort-key=}'[sort by key]:key:( ${(f)"$(_call_program sort-keys $words[1] --sort-key help)"} )' \
  '(-t --tree)'{-t,--tree}'[show tree view (can be combined with -s)]' \
  '(-u --user)'{-u+,--user=}'[show processes of user]: : _users' \
  '(-U --no-unicode)'{-U,--no-unicode}'[Do not use unicode but plain ASCII]' \
  '--readonly''[Disable all system and process changing features]' \
  '(-)'{-V,--version}'[Print version information]'
